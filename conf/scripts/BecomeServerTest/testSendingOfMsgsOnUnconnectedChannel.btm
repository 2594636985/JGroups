

## Test case for https://issues.jboss.org/browse/JGRP-1522
## - Join A
## - Try to join B
##   - After the stack is started (so that NAKACK2 can receive messages), but before connect() returns,
##     a message is multicast from A
##   - B's NAKACK2 receives the message and queues it as is_server=false
##   - On installation of view {A,B}, the queued message is delivered
##   - B sends a response message in the *same thread*
##   --> This will trigger an exception as the channel is yet unconnected
##       (will be set to 'connected' after connect() returns)


## The rendezvous that signals the sender on A to send its message (the sender on A is blocked until the main
## test signals it)
RULE CreateRendezvous
CLASS BecomeServerTest
METHOD testSendingOfMsgsOnUnconnectedChannel
IF TRUE
   DO System.out.println("--> Creating rendezvous");
      createRendezvous("BecomeServerTest",  2, true);
ENDRULE


## Blocks the sender on A from sending a message until signalled
RULE BlockSenderOnRendezvousEntry
CLASS BecomeServerTest
METHOD sendMessage
AT ENTRY
IF rendezvous("BecomeServerTest") != -1
    DO System.out.println("--> A: before sending message");
ENDRULE


## Blocks the sender on A from sending a message until signalled
RULE BlockSenderOnRendezvousExit
CLASS BecomeServerTest
METHOD sendMessage
AT EXIT
IF rendezvous("BecomeServerTest") != -1
    DO System.out.println("--> A: after sending message");
ENDRULE


RULE TestSendingOfMsgsOnUnconnectedChannel
CLASS ClientGmsImpl
METHOD installView
AT ENTRY
BIND local_addr_name=org.jgroups.util.UUID.get($0.gms.local_addr);
IF local_addr_name.equals("B") AND rendezvous("BecomeServerTest") != -1
   DO System.out.println("--> [B] installView(): entered rendezvous");
      rendezvous("BecomeServerTest");
ENDRULE
